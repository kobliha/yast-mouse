/*
 *
 * Module:             mouse.ycp
 *
 * Author:             Thomas Roelz (tom@suse.de)
 *
 * Submodules:
 *
 *
 * Purpose:	configure mouse in running system
 *
 * Modify:
 *
 *
 * $Id$
 */

{
    textdomain "mouse";

    import "Mode";

    import "Wizard";
    include "ui/common_popups.ycp";
    include "ui/common_functions.ycp";
    include "ui/common_messages.ycp";

    // Memorize the current mouse.
    //
    string mouse_on_entry = Mouse::mouse;

    // Check if this is a reconfiguration run.
    //
    if ( Mode::reprobe )
    {
	// Reprobe mouse module to achieve same behaviour as during installation.
	//
	Mouse::Probe();		// Do the best we can...
	Mouse::Set( Mouse::mouse );

	y2milestone("Reprobed mouse");
    }

    // create the wizard dialog
    //
    Wizard::CreateDialog();

    any result = nil;

    // On import the mouse module has read the sysconfig values and is initialized.
    // Let the user choose a new mouse.
    //
    result = WFM::CallFunction ( `inst_mouse( true, true ) );

    if ( result == `next )
    {
	// User accepted the the setting.
	// Only if the user has chosen a different mouse change the system configuration.
	//
	if ( mouse_on_entry != Mouse::mouse )
	{
	    y2milestone("User selected new mouse: <%1>", Mouse::mouse );

	    // Save changes...
	    //
	    Mouse::Save();
	}
	else
	{
	    y2milestone( "Mouse not changed --> doing nothing" );
	}
    }
    else	// `cancel or `back
    {
	y2milestone( "User cancelled --> no change" );
    }

    return UI::CloseDialog();
}
